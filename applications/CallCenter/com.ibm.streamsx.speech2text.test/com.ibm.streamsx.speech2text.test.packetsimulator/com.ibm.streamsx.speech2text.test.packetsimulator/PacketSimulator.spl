namespace com.ibm.streamsx.speech2text.test.packetsimulator ;

use com.ibm.streamsx.network.ipv4::* ;
use com.ibm.streamsx.network.ipv6::* ;
use com.ibm.streamsx.network.source::* ;
use com.ibm.streamsx.network.parse::* ;

composite PacketSimulator
{
	param
		expression<rstring> $connHost     : getSubmissionTimeValue("connHost");
	       	expression<uint32>  $connPort     : (uint32) getSubmissionTimeValue("connPort");
       		expression<rstring> $PcapFileDir : getSubmissionTimeValue("pcapFileDir");
	type
	    PacketType =
	        uint64 packetsReceived,             // total number of packet received, as of last metrics interval
	        uint64 packetsDropped,              // total number of packet dropped, as of last metrics interval
	        uint64 packetsProcessed,            // total number of packets processed by operator, so far 
	        rstring ipSrcAddr,
	        uint32 captureSeconds, 
	        uint8 IP_PROTOCOL,                  // IP protocol: 0x01==1 for ICMP, 0x6==6 for TCP, 0x11==17 for UDP
	        uint16 IP_DST_PORT,
	        uint32 PAYLOAD_LENGTH,              // length of packet payload, following all network headers
	        blob PAYLOAD_DATA;                  // contents of packet payload, following all network headers
	graph
		
        stream<rstring filename> TestFile = DirectoryScan()
    	{
    		param
    		   directory : $PcapFileDir ;
		   pattern : "\\.pcap$";
		   initDelay : 3.0; 
    	}

		
		stream<PacketType> PacketStream as Out = PacketFileSource(TestFile)
		{
			output
				Out : ipSrcAddr = convertIPV4AddressNumericToString(IPV4_SRC_ADDRESS()),
					IP_DST_PORT = IP_DST_PORT(), IP_PROTOCOL = IP_PROTOCOL(),
					captureSeconds =(uint32) getSeconds(getTimestamp()), PAYLOAD_DATA =
					PAYLOAD_DATA(), PAYLOAD_LENGTH = PAYLOAD_LENGTH() ;
		}

		() as TcpSink_PacketStream = TCPSink(PacketStream)
		{
			param
				role : server ;
				port : $connPort ;
				name : $connHost ;
				format : csv ;
			config
				threadedPort : queue(PacketStream, Sys.Wait, 1000000) ;
		}

}
